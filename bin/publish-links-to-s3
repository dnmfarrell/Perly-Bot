#!/usr/bin/env perl
use strict;
use warnings;
use Getopt::Long 'GetOptions';
use File::MimeInfo::Magic 'mimetype';
use Path::Tiny 'path';
use Paws;

GetOptions(
  'bucket=s' => \ my $bucket,
  'prefix=s' => \ my $prefix,
  'region=s' => \(my $region = 'us-east-1'),
  'acl=s'    => \(my $acl    = 'public-read'),
) or die 'unrecognized options: ' . join ' ', @ARGV;

my $s3 = Paws->service('S3', region => $region);

for my $file (@ARGV) {
  my $pt  = path($file);
  my $key = $prefix ? join('/',$prefix,$pt->basename) : $pt->basename;
  $s3->PutObject(
    ContentType => mimetype($file),
    Bucket      => $bucket,
    Body        => $pt->slurp,
    Key         => $key,
    ACL         => $acl,
  );
}
